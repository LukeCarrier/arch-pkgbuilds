# Maintainer: Luke Carrier <luke@carrier.im>
# Contributor: Jerome Leclanche <jerome@leclan.ch>

pkgname=ttf-cascadia-code
_name=cascadia-code
pkgver=1911.21
pkgrel=1
pkgdesc="A monospaced font by Microsoft that includes programming ligatures"
arch=("any")
url="https://github.com/microsoft/cascadia-code"
license=("custom:OFL")
depends=("fontconfig" "xorg-font-utils")

source=(
    "$_name-$pkgver.tar.gz::https://github.com/microsoft/cascadia-code/archive/v$pkgver.tar.gz"
    "$_name-$pkgver-Cascadia.ttf::https://github.com/microsoft/cascadia-code/releases/download/v$pkgver/Cascadia.ttf"
    "$_name-$pkgver-CascadiaMono.ttf::https://github.com/microsoft/cascadia-code/releases/download/v$pkgver/CascadiaMono.ttf"
    "$_name-$pkgver-CascadiaMonoPL.ttf::https://github.com/microsoft/cascadia-code/releases/download/v$pkgver/CascadiaMonoPL.ttf"
    "$_name-$pkgver-CascadiaPL.ttf::https://github.com/microsoft/cascadia-code/releases/download/v$pkgver/CascadiaPL.ttf"
)
sha512sums=(
    '8eea9fae5444e297cf1d5927f7e71f0d150a1970cd37dde9fffe58b47fe0b357470d0c327e6de42f5c66227d3e0b8260c0b6308b3284251c350a00badb81de48'
    '8e2b55eb589bedb03c18857f6852333469776a38705ba3722ffa9fe4a1a8e82cf52d74ce1bbe749d06179ed74136eb0f419b7467e92e935f35c0966928810349'
    '69b95ba8bf2c23a050e78e08acfcfa22b09efb20a6b8fe6ef1d9595484686095811f37ecc47c690a49278de2a0fec03bdc4ec3b37a49180381c87ad6abfc5f8d'
    'cd638e171621844d9227d4612a5674c029aace29a52e85624e2c4ab719bf3911a74d721d852a18a4116eaedd2d952239630e3b4e5ec887e2a12d83e67e60b042'
    '9c171c0b98801f7a8236fd5e88f849c97de8d66c4357e3a27c0d5c2b0b64d459854283251c4478ff62062851423b1f84e0912a68be0eb9f02dc525af90251a5e'
)


package() {
    install -Dm644 "$srcdir/$_name-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
    for variant in 'Cascadia' 'CascadiaMono' 'CascadiaMonoPL' 'CascadiaPL'; do
        install -Dm644 "$srcdir/$_name-$pkgver-$variant.ttf" "$pkgdir/usr/share/fonts/TTF/$variant.ttf"
    done
}
